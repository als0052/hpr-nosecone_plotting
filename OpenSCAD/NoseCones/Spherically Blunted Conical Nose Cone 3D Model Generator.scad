/* Spherically Blunted Concial Nose Cone 3D Model Generator
 *
 * NOTE: The performance, safety, and structural integrity of
 *       3D models created using this program are entirely the
 *       responsibility of the user. 
 *
 * NOTE: This is the version without a hollow interior.
 *
 * If 3D printers using PLA, ABS or any other type of low
 * temperature plastic are used to print any models generated by
 * this program, keep in mind that these plastics, especially PLA,
 * become very weak at relatively low temperatures, temperatures
 * as low as the interior of a sunlit car. Even with this in mind,
 * any 3D printed items printed using low temperature 3D printer
 * plastics are intended for use in low power rocketry only and
 * should be printed with a high internal fill and high wall
 * thickness settings (set by the 3D printer application).
 *
 * Some primers and paints can weaken common 3D printed plastics.
 * Do your research before using primers and paints!
 * 
 * If models are printed for mid and high power rocketry
 * applications they should only serve as formers for fiberglass
 * or carbon fiber layup. I have no idea how well PLA or ABS stand
 * up to mold release agents or how well they will adhere to the
 * fiberglass or carbon fiber if the 3D printed model is not
 * extracted and is instead left in place
 *
 * If there's anything I've said above you don't understand, you
 * shouldn't be creating parts with this program for rockets you 
 * intend to fly.
 *
 * NOTE: All dimensions should be given in MILLIMETERS. All angles should be 
 *       given in DEGREES.
 *
 * Created by Unknown
 * Created on Unknown
 * Updated on 04-19-2023 by A. Smelser
 */
// Render quality setting
$fn = 0;
$fa = 0.01;
$fs = 1.0;  // 2 (coarse) to 0.3 (fine)

// Bottom shoulder dimensions 
shl_len = 30;  // Shoulder length
shl_dia = 28.8;  // Shoulder diameter (I.D. of body tube)

// Bottom shoulder anchor hole dimensions 
anc_dep = 15;  // Anchor hole depth (set to 0 for no hole)
anc_dia = 9.5;  // Anchor hole diameter (set to 0 for no hole)

btm_dia = 30.7;  // Conical section bottom diameter. (O.D. of body tube)
con_len = 80;  // Set conical section length
top_dia = 10;  // Set conical section top diameter

// Build nosecone shoulder
difference(){
    cylinder(h=shl_len, d=shl_dia);  // create bottom shoulder
    cylinder(h=anc_dep, d=anc_dia);  // create anchor hole
}

// Calculate radius and tangent point offset for sphere
theta = atan((btm_dia-top_dia)/(2*con_len));
top_rad = top_dia/2;
offset = tan(theta)*top_rad;
sph_rad = sqrt(top_rad*top_rad+offset*offset);

// Build nosecone body
union(){
	translate([0, 0, shl_len])
	cylinder(h=con_len, d1=btm_dia, d2=top_dia);  // create cone
	translate([0, 0, shl_len+con_len-offset])
	sphere(r=sph_rad);  // create spherical tip
}
