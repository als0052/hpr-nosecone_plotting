// Power Series Nose Cone 3D Model Generator
// thanks to "jsc" for the sweet polygon creation technique

// NOTE: The performance, safety, and structural integrity of
// 3D models created using this program are entirely the
// responsibility of the user. 
//
// If 3D printers using PLA, ABS or any other type of low
// temperature plastic are used to print any models generated by
// this program, keep in mind that these plastics, especially PLA,
// become very weak at relatively low temperatures, temperatures
// as low as the interior of a sunlit car. Even with this in mind,
// any 3D printed items printed using low temperature 3D printer
// plastics are intended for use in low power rocketry only and
// should be printed with a high internal fill and high wall
// thickness settings (set by the 3D printer application).
//
// Some primers and paints can weaken common 3D printed plastics.
// Do your research before using primers and paints!
// 
// If models are printed for mid and high power rocketry
// applications they should only serve as formers for fiberglass
// or carbon fiber layup. I have no idea how well PLA or ABS stand
// up to mold release agents or how well they will adhere to the
// fiberglass or carbon fiber if the 3D printed model is not
// extracted and is instead left in place
//
// If there's anything I've said above you don't understand, you
// shouldn't be creating parts with this program for rockets you 
// intend to fly.

// Leave these alone
$fn = 0;
$fa = 0.01;

// User modifiable items marked with * in comments

// Render quality setting
$fs = 0.3;	// * range: 2 (coarse) to 0.3 (fine)

// Power Series type selection
// * for 1/2 Power set n = 1/2
// * for 3/4 Power set n = 3/4
// * for Conical set n = 1
n = 1/2;

// All dimensions should be in MILLIMETERS

// Set shoulder length and diameter
shl_len = 35;	// * shoulder length
shl_dia = 28.8;	// * shoulder diameter

// Set anchor hole diameter and depth 
anc_dep = 20; 	// * anchor hole depth
anc_dia = 9.5;	// * anchor hole diameter

// Set nosecone body base diameter and aspect ratio (fineness)
bdy_dia = 30.7;	// * body base diameter
asp_rat = 5;	// * body aspect ratio (Length/Diameter)

// Build nosecone shoulder
translate ([0, 0, -shl_len])
difference(){
    cylinder (h=shl_len, r=shl_dia/2);    // create shoulder
    cylinder (h=anc_dep, r=anc_dia/2);    // create anchor hole
}

// Number of longitudinal faces to calculate
num_fcs = 400;  // * default = 400, finer resolution with increase

// Build nosecone body
bdy_len = bdy_dia*asp_rat;	// body length
bdy_rad = bdy_dia/2;		// body base radius
function y(x) = bdy_rad*pow((x/bdy_len),n);
ply_pts = concat([[0, 0]],[for (i=[0:num_fcs]) let (x=(i*bdy_len/num_fcs))[y(bdy_len-x), x]]);
rotate_extrude() polygon(points = ply_pts);